plugins {
    alias(libs.plugins.java)
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url = uri('https://repository.aspose.com/repo/')
    }
}

java {
    sourceCompatibility = "1.8"
    targetCompatibility = "${JAVA_VERSION}"
    toolchain {
        languageVersion = JavaLanguageVersion.of("${JAVA_VERSION}")
    }
}

dependencies {
    implementation(libs.aspose.html) {
        artifact {
            classifier = "jdk${JAVA_VERSION}"
        }
    }

    implementation(
            libs.commons.lang3,
            libs.bundles.junit
    )
}

tasks.register('SimpleWait', JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'com.aspose.html.examples.SimpleWait'
}

test {
    description = 'Runs the tests in the examples source set'
    group = 'verification'
    testClassesDirs = sourceSets.main.output.classesDirs
    classpath = sourceSets.main.runtimeClasspath
    useJUnitPlatform() // Enables JUnit 5 (Jupiter)
    maxParallelForks = Runtime.runtime.availableProcessors()
    testLogging.showStandardStreams = false
}